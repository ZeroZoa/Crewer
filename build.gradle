plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'NPJ'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'


	// Database Drivers
	implementation 'org.postgresql:postgresql:42.7.7'


	runtimeOnly 'com.h2database:h2'

	// JWT Dependencies
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Hibernate Commons Annotations (Hibernate 6 호환성 문제 해결)
	implementation 'org.hibernate.common:hibernate-commons-annotations:6.0.6.Final'

	// Test Dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.amqp:spring-rabbit-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	//QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"

	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"

}
def generatedDir = 'src/main/generated' // 1. Q-클래스 생성 위치를 변수로 관리

sourceSets {
	main.java.srcDirs += [generatedDir] // 2. main 소스셋에 생성된 Q-클래스 경로 추가
}

// 3. compileJava 작업 시 Q-클래스를 생성하도록 설정
tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(generatedDir))
}

// 4. clean 작업 시 생성된 Q-클래스 폴더도 함께 삭제
tasks.named('clean') {
	doLast {
		file(generatedDir).deleteDir()
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Q-클래스 저장 경로 설정
//querydsl {
//	jpa = true
//	querydslSourcesDir = 'src/main/generated' // Q-클래스를 저장할 폴더
//}
//sourceSets {
//	main {
//		java {
//			srcDirs += [file('src/main/generated')]
//		}
//	}
//}
//
//tasks.named('test') {
//	useJUnitPlatform()
//}